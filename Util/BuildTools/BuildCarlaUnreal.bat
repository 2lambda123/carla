@echo off
setlocal enabledelayedexpansion

rem BAT script that creates the binaries for Carla (carla.org).
rem Run it through a cmd with the x64 Visual C++ Toolset enabled.

set LOCAL_PATH=%~dp0
set FILE_N=-[%~n0]:

rem Print batch params (debug purpose)
echo %FILE_N% [Batch params]: %*

rem ============================================================================
rem -- Parse arguments ---------------------------------------------------------
rem ============================================================================

set BUILD_CARLA_UNREAL=false
set LAUNCH_UNREAL_EDITOR=false
set REMOVE_INTERMEDIATE=false
set USE_CARSIM=false
set USE_CHRONO=false
set CARSIM_STATE="CarSim OFF"
set CHRONO_STATE="Chrono OFF"
set EDITOR_FLAGS=""

:arg-parse
echo %1
if not "%1"=="" (
    if "%1"=="--editor-flags" (
        set EDITOR_FLAGS=%2
        shift
    )
    if "%1"=="--build" (
        set BUILD_CARLA_UNREAL=true
    )
    if "%1"=="--launch" (
        set LAUNCH_UNREAL_EDITOR=true
    )
    if "%1"=="--clean" (
        set REMOVE_INTERMEDIATE=true
    )
    if "%1"=="--carsim" (
        set USE_CARSIM=true
    )
    if "%1"=="--chrono" (
        set USE_CHRONO=true
    )
    if "%1"=="-h" (
        goto help
    )
    if "%1"=="--help" (
        goto help
    )
    shift
    goto arg-parse
)
rem remove quotes from arguments
set EDITOR_FLAGS=%EDITOR_FLAGS:"=%

if %REMOVE_INTERMEDIATE% == false (
    if %LAUNCH_UNREAL_EDITOR% == false (
        if %BUILD_CARLA_UNREAL% == false (
            goto help
        )
    )
)

rem Get Unreal Engine root path
if not defined CARLA_UE_ROOT (
    set KEY_NAME="HKEY_LOCAL_MACHINE\SOFTWARE\EpicGames\Unreal Engine"
    set VALUE_NAME=InstalledDirectory
    for /f "usebackq tokens=1,2,*" %%A in (`reg query !KEY_NAME! /s /reg:64`) do (
        if "%%A" == "!VALUE_NAME!" (
            set CARLA_UE_ROOT=%%C
        )
    )
    if not defined CARLA_UE_ROOT goto error_unreal_no_found
)
if not "%CARLA_UE_ROOT:~-1%"=="\" set CARLA_UE_ROOT=%CARLA_UE_ROOT%\

rem Set the visual studio solution directory
rem
set CARLA_UNREAL_PROJECT_FOLDER=%ROOT_PATH:/=\%Unreal\CarlaUnreal\
pushd "%CARLA_UNREAL_PROJECT_FOLDER%"

rem Clear binaries and intermediates generated by the build system
rem
if %REMOVE_INTERMEDIATE% == true (
    rem Remove directories
    for %%G in (
        "%CARLA_UNREAL_PROJECT_FOLDER%Binaries",
        "%CARLA_UNREAL_PROJECT_FOLDER%Build",
        "%CARLA_UNREAL_PROJECT_FOLDER%Saved",
        "%CARLA_UNREAL_PROJECT_FOLDER%Intermediate",
        "%CARLA_UNREAL_PROJECT_FOLDER%Plugins\Carla\Binaries",
        "%CARLA_UNREAL_PROJECT_FOLDER%Plugins\Carla\Intermediate",
        "%CARLA_UNREAL_PROJECT_FOLDER%.vs"
    ) do (
        if exist %%G (
            echo %FILE_N% Cleaning %%G
            rmdir /s/q %%G
        )
    )

    rem Remove files
    for %%G in (
        "%CARLA_UNREAL_PROJECT_FOLDER%CarlaUnreal.sln"
    ) do (
        if exist %%G (
            echo %FILE_N% Cleaning %%G
            del %%G
        )
    )
)

rem Download Houdini Plugin

set HOUDINI_PLUGIN_REPO=https://github.com/sideeffects/HoudiniEngineForUnreal.git
set HOUDINI_PLUGIN_PATH=Plugins/HoudiniEngine
set HOUDINI_PLUGIN_BRANCH=Houdini19.5-Unreal5.00
set HOUDINI_PATCH=${CARLA_UTIL_FOLDER}/Patches/houdini_patch.txt
if not exist "%HOUDINI_PLUGIN_PATH%" (
  call git clone -b %HOUDINI_PLUGIN_BRANCH% %HOUDINI_PLUGIN_REPO% %HOUDINI_PLUGIN_PATH%
)

rem Build Carla Editor
rem

if %USE_CARSIM% == true (
    py -3 %ROOT_PATH%Util/BuildTools/enable_carsim_to_uproject.py -f="%ROOT_PATH%Unreal/CarlaUnreal/CarlaUnreal.uproject" -e
    set CARSIM_STATE="CarSim ON"
) else (
    py -3 %ROOT_PATH%Util/BuildTools/enable_carsim_to_uproject.py -f="%ROOT_PATH%Unreal/CarlaUnreal/CarlaUnreal.uproject"
    set CARSIM_STATE="CarSim OFF"
)
if %USE_CHRONO% == true (
    set CHRONO_STATE="Chrono ON"
) else (
    set CHRONO_STATE="Chrono OFF"
)
set OPTIONAL_MODULES_TEXT=%CARSIM_STATE% %CHRONO_STATE%
echo %OPTIONAL_MODULES_TEXT% > "%ROOT_PATH%Unreal/CarlaUnreal/Config/OptionalModules.ini"

if %BUILD_CARLA_UNREAL% == true (
    echo %FILE_N% Building Unreal Editor...

    call "%CARLA_UE_ROOT%Engine\Build\BatchFiles\Build.bat"^
        CarlaUnrealEditor^
        Win64^
        Development^
        -WaitMutex^
        -FromMsBuild^
        "%ROOT_PATH%Unreal/CarlaUnreal/CarlaUnreal.uproject"
    if errorlevel 1 goto bad_exit

    call "%CARLA_UE_ROOT%Engine\Build\BatchFiles\Build.bat"^
        CarlaUnreal^
        Win64^
        Development^
        -WaitMutex^
        -FromMsBuild^
        "%ROOT_PATH%Unreal/CarlaUnreal/CarlaUnreal.uproject"
    if errorlevel 1 goto bad_exit
)

rem Launch Carla Editor
rem
if %LAUNCH_UNREAL_EDITOR% == true (
    echo %FILE_N% Launching Unreal Editor...
    call "%CARLA_UE_ROOT%\Engine\Binaries\Win64\UnrealEditor.exe"^
        "%CARLA_UNREAL_PROJECT_FOLDER%CarlaUnreal.uproject" %EDITOR_FLAGS%
    if %errorlevel% neq 0 goto error_build
)

goto good_exit

rem ============================================================================
rem -- Messages and Errors -----------------------------------------------------
rem ============================================================================

:help
    echo Build LibCarla.
    echo "Usage: %FILE_N% [-h^|--help] [--build] [--launch] [--clean]"
    goto good_exit

:error_build
    echo.
    echo %FILE_N% [ERROR] There was a problem building CarlaUnreal.
    echo %FILE_N%         Please go to "Carla\Unreal\CarlaUnreal", right click on
    echo %FILE_N%         "CarlaUnreal.uproject" and select:
    echo %FILE_N%         "Generate Visual Studio project files"
    echo %FILE_N%         Open de generated "CarlaUnreal.sln" and try to manually compile it
    echo %FILE_N%         and check what is causing the error.
    goto bad_exit

:good_exit
    endlocal
    exit /b 0

:bad_exit
    endlocal
    exit /b %errorlevel%

:error_unreal_no_found
    echo.
    echo %FILE_N% [ERROR] Unreal Engine not detected
    goto bad_exit
